#!/bin/csh -f

if ( ! -e ota-passphrase ) then
  echo "You must create a file called ota-passphrase that contains your secret passphrase for generate OTA bundles with a fixed BID"
  exit 3
endif

set gitmessage=`git log | head -1 ; echo "[" ; git diff --shortstat ; echo " @ "; date ; echo "]" `
set gitmessage=`echo "$gitmessage" | sed -e 's,/,,g'`
echo GIT message =  "$gitmessage"

set path=($path `pwd`/serval-dna)

mkdir -p instance
cd instance
setenv SERVALINSTANCE_PATH `pwd`
set pwdlen=`pwd | wc -c `
if ( $pwdlen > 70 ) then
  setenv SERVALINSTANCE_PATH /tmp/me-ota-`ps -ef | md5sum | cut -c1-4`
  echo "WARNING: Path is too long. Trying to use temporary directory $SERVALINSTANCE_PATH as instance path"
  set cleanuptmp=1
endif
cd ..

servald stop
if ( -e $SERVALINSTANCE_PATH/rhizome.db ) then
  rm $SERVALINSTANCE_PATH/rhizome.db
endif
touch $SERVALINSTANCE_PATH/serval.conf  # silence warnings about missing conf file
servald start

set keys=`servald keyring list | wc -l`
echo "-----------"
servald keyring list
echo "-----------"
if ( $keys < 3 ) then
  echo "Creating keyring entry."
  servald keyring add
endif

set sid=`servald keyring list | tail -1 | cut -f1 -d:`
echo "My SID is $sid"

if ( "x$sid" == "x" ) then
  echo "ERROR: Could not get SID for servald instance"
  exit 3
endif

# Now create OTA update file

echo "-----------"
echo "Creating bundle payload"
echo "-----------"
# Start by including the files to be updated (currently just
# the main binaries, we can add more stuff later)
if ( -e staging ) then
  rm -fr staging
endif
mkdir staging
cp helpdesk.sid monitor.sid staging/
# openwrt mesh extender packages
cp ../bin/ar71xx/packages/serval/*.ipk staging/
set IPKFILES=`( cd ../bin/ar71xx/packages/serval ; echo *.ipk )`
echo "Package files: $IPKFILES"

( cd staging/ ; tar zcvf - . ) > files.tgz
if ( -e ota-complete ) then
  rm ota-complete
endif
cat ota-shell | sed -e "s/GITCOMMITSTUFF/${gitmessage}/" -e "s/IPKFILES/${IPKFILES}/" > ota-shell-subst

dd if=/dev/zero of=ota-complete bs=8192 count=1
dd conv=notrunc seek=0 if=ota-shell-subst of=ota-complete bs=8192 count=1
cat files.tgz >> ota-complete

if ( -e me-ota-update.manifest ) then
  rm me-ota-update.manifest
endif

echo "-----------"
echo "Verify that Rhizome database is empty"
echo "-----------"
servald rhizome list

echo "-----------"
echo "Insert new bundle"
echo "-----------"
set passphrase=`cat ota-passphrase`
echo servald rhizome add file --force-new $sid ota-complete me-ota-update.manifest "#${passphrase}"
servald rhizome add file --force-new $sid ota-complete me-ota-update.manifest "#${passphrase}"

echo "-----------"
echo "Verify that new bundle has been inserted"
echo "-----------"

servald rhizome list
servald stop

set bundleid=`servald rhizome list | tail -1 | cut -f3 -d:`
echo "BID = $bundleid"

# Create bundleid file for OTA update
echo $bundleid > otabid.txt

if ( "x$cleanuptmp" != "x" ) then
  find $SERVALINSTANCE_PATH -ls
  rm -fr $SERVALINSTANCE_PATH
endif

